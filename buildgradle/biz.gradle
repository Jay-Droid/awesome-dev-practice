//****************************************
//******  常规业务组件公共gradle配置  *******
//****************************************
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    //模块中启用ViewBinding
    //谷歌在 Android Studio 3.6 Canary 11 及更高版本中加入了新的视图绑定工具 ViewBinding。
    viewBinding {
        enabled = true
    }
    // Android Plugin for Gradle 1.5.0+
    dataBinding {
        enabled true
    }
    //给 Module 内的资源名增加前缀, 避免资源名冲突
    resourcePrefix "${project.name.toLowerCase().replaceAll("-", "_")}_"

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        //Instrumentation Test class
        testInstrumentationRunner rootProject.ext.androidJUnitRunner
        //consumerProguardFiles  打包库Module发布AAR时配置的混淆文件，如果没有配置这个属性，在其他项目引用AndroidLib 项目且开启minifyEnabled时或许会报错，因为主项目不会自主引入依赖库的ProguardFile文件，必须在主项目中配置consumerProguardFiles节点，这样主项目就会引入依赖库的gradle文件中的（默认为release变体）下ProguardFile。
        consumerProguardFiles 'consumer-rules.pro'
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
        //X5兼容64位手机
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility rootProject.ext.javaSourceCompatibility
        sourceCompatibility rootProject.ext.javaSourceCompatibility
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.javaSourceCompatibility
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.kotlinStdlibJdk8
    implementation rootProject.ext.appcompat
    implementation rootProject.ext.coreKtx
    testImplementation rootProject.ext.junit
    androidTestImplementation rootProject.ext.junitX
    androidTestImplementation rootProject.ext.espressoCore
    implementation rootProject.ext.constraintLayout

    implementation project(path: ':base:base-common')
    //需要所有使用 ARouter 的组件都添加依赖
    kapt rootProject.ext.arouterCompiler
    //Arouter
    implementation rootProject.ext.arouterApi
    implementation rootProject.ext.recyclerAdapterHelper

}
